archetype asset_ownership (owner : address)

variable assetid : bytes =
  0x68746ecbcd72793aefda48f1b67a3190fc380a7633055d2336fb90cd990582a2

variable bestbidder : address = owner
variable bestbid    : tez = 0tz

variable endofbid   : date = now

states =
| Owned initial
| ForSale

transition upforsale (price : tez) {
   called by owner
   from Owned to ForSale
   with effect {
      bestbid := price;
      endofbid := now + 5m;
   }
}

entry bid() {
   require {
      r1 otherwise "Asset Not For Sale": state = ForSale;
      r2: now < endofbid;
      r3: caller <> bestbidder;
      r4: transferred > bestbid;
   }
   effect {
     if balance <> transferred then
       transfer bestbid to bestbidder;
     bestbidder := caller;
     bestbid := transferred;
     endofbid += 2m;
   }
}

transition claim () {
  require { r5 otherwise "Bid Period Is Still On": now > endofbid }
  from ForSale to Owned
  with effect {
     if balance > 0tz then
         transfer balance to owner;
     owner := bestbidder;
  }
}
